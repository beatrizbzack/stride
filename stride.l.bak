
%{
#include "stride.tab.h"
#include <stdlib.h>
#include <string.h>

extern int yylineno;
void yyerror(const char *s);
%}

%option noyywrap

%%

[ \t\r\n]+                 { /* ignore whitespace */ }

"//".*                     { /* line comment */ }

"/*"([^*]|\*+[^*/])*\*+"/" { /* block comment */ }

":="                       { return ASSIGN; }

"=="                       { return EQ; }
"!="                       { return NEQ; }
"<="                       { return LE; }
">="                       { return GE; }

"<"                        { return '<'; }
">"                        { return '>'; }

"+"                        { return '+'; }
"-"                        { return '-'; }
"*"                        { return '*'; }
"/"                        { return '/'; }

\"([^\\\"]|\\.)*\"         {
                             /* remove surrounding quotes and copy */
                             size_t len = yyleng;
                             char *raw = (char*)malloc(len-1);
                             strncpy(raw, yytext+1, len-2);
                             raw[len-2] = '\0';
                             yylval.str = raw;
                             return STRING;
                           }

[0-9]+                     {
                             yylval.num = atoi(yytext);
                             return NUMBER;
                           }

[a-zA-Z_][a-zA-Z0-9_]*     {
                             /* keywords */
                             if (strcmp(yytext, "if") == 0) return IF;
                             if (strcmp(yytext, "then") == 0) return THEN;
                             if (strcmp(yytext, "else") == 0) return ELSE;
                             if (strcmp(yytext, "end") == 0) return END;
                             if (strcmp(yytext, "while") == 0) return WHILE;
                             if (strcmp(yytext, "do") == 0) return DO;

                             if (strcmp(yytext, "corra") == 0) return CORRA;
                             if (strcmp(yytext, "pare") == 0) return PARE;
                             if (strcmp(yytext, "espere") == 0) return ESPERE;
                             if (strcmp(yytext, "mostre") == 0) return MOSTRE;

                             /* identifiers */
                             yylval.str = strdup(yytext);
                             return IDENTIFIER;
                           }

. {
    unsigned char c = (unsigned char) yytext[0];
    char buf[64];
    snprintf(buf, sizeof(buf), "Token inv√°lido: 0x%02X", c);
    yyerror(buf);
    /* skip the character (no return) */
}

%%

